/*=========================================================
Variant Mining
by Mumbles
===========================================================
Idea: http://goo.gl/ndXr6R
===========================================================
Description:
A simple mining system; allows for interacting players to
excavate minerals by "mining" them with special equipment
and tools. Minerals disappear and respawn randomly; chance
is determined by configuration.

Configuration is mostly done in arrays so that settings may
be changed with no modifications to the script. Duplicate 
in additional locations as needed; update the value of
'.var_amount' correspondingly.
===========================================================
Compatibility:
Optimised for Hercules emulators.
===========================================================
Changelog:
v1.0 - First version.
	v1.0.1 - Fixed issue with success algorithm.
	v1.0.2 - Added randomised debris.
	v1.0.3 - Added additional locations in Payon Cave.
v2.0 - Optimised for Hercules emulators.
	v2.0.1 - Updated algorithms.
	v2.0.2 - Added randomised disappearing and respawning.
	v2.0.3 - Updated version format.
	v2.0.4 - Tradução pt_BR by Linc
=========================================================*/

-	script	Variant Mineral#0::mining	S_EMPEL_1,{
	
	/*-----------------------------------------------------
	Script
	-----------------------------------------------------*/
	// Verifica o equipamento
	for (.@i = 0; .@i < getarraysize(.equip); .@i++) {
		if (!isequipped(.equip[.@i])) {
			message strcharinfo(0), "Você precisa ter '"+ getitemname(.equip[.@i]) +"' equipado para minerar!";
			.@unequipped++;
		}
	}
	
	// Show count of equipment not worn
	if (.@unequipped) {
		message strcharinfo(0), .@unequipped +" de "+ getarraysize(.equip) +" equipamentos não foram equipados.";
		end;
	}
	
	// Check tools
	for (.@i = 0; .@i < getarraysize(.tool); .@i += 2) {
		if (countitem(.tool[.@i]) < .tool[.@i + 1]) {
			//message strcharinfo(0), "Você precisa de "+ .tool[.@i + 1] +" "+ getitemname(.tool[.@i]) +" para minerar!";
			.@gearless++;
		}
	}
	
	// Show count of tools not in inventory
	if (.@gearless) {
		.@grammar$ = (getarraysize(.tool) > 1 ? " Ferramenta não localizada." : "");
		message strcharinfo(0), .@gearless + .@grammar$ +" Você precisa de uma Picareta Velha.";
		end;
	}
	
	// Progress
	message strcharinfo(0), "Minerando...";
	progressbar "green", .progress;
	
	// Delete tools
	for (.@i = 0; .@i < getarraysize(.tool); .@i += 2) {
		delitem .tool[.@i], .tool[.@i + 1];
	}
	
	// Audio/visual effects
	soundeffect .sound$, 0;
	specialeffect .effect;
	
	// Drop random debris
	getmapxy(.@m$, .@x, .@y, 0);
	.@scatter = rand(1, .limit);
	for (.@i = 0; .@i < .@scatter; .@i++) {
		.@random = rand(2) * (!rand(1) ? -1 : 1);
		makeitem .debris, 1, .@m$, .@x + .@random, .@y + .@random;
	}
	
	// Randomize target mineral to mine
	do {
		.@target = rand(getarraysize(.mineral));
	} while (.@target % 2);
	
	// Max range of success
	.@range = .success[.@target + 1];
	
	// Check if failed
	if (rand(1, .@range) != .success[.@target]) {
		message strcharinfo(0), "Não foi encontrado nada nesse mineral...";
	} else {
		// Get mineral(s)
		getitem .mineral[.@target], .mineral[.@target + 1];
		message strcharinfo(0), "Você conseguiu minerar com sucesso. ";
	}
	
	// Hide mineral
	if (!rand(.hide_chance)) {
		hideonnpc strnpcinfo(3);
	}
	
	end;
	
	
	/*-----------------------------------------------------
	Timer
	-----------------------------------------------------*/	
	OnMinute00:
		// Respawn minerals
		if (gettime(3) % .respawn_rate) {
			for (.@i = 1; .@i <= .var_amount; .@i++) {
				if (!rand(.respawn_chance)) {
					hideoffnpc strnpcinfo(1) +"#"+ .@i;
				}
			}
		}
		
		end;
	
	
	/*-----------------------------------------------------
	Configuration
	-----------------------------------------------------*/
	OnInit:
		// Minerals (constant/ID, amount)
		setarray .mineral[0], 6226, 1, 6225, 1, 7619, 1, 7620, 1;
		
		// Equipment (constant/ID)
		setarray .equip[0], Safety_Helmet, Gas_Mask;
		
		// Tools (constant/ID, amount)
		setarray .tool[0], Old_Pick, 1;
		
		// Mining time (in seconds)
		.progress = 1;
		
		// Audio/visual effects
		.sound$ = "chepet_attack.wav";
		.effect = EF_HIT5;
		
		// Debris
		.debris = Stone;	// Stone
		.limit = 2;			// Scatter limit
		
		// Hiding and Respawning
		.var_amount = 44;		// Amount of Variant Minerals
		.hide_chance = 2;		// Chance in x to hide (default: 4 [25%])
		.respawn_chance = 2;	// Chance in x to respawn (default: 2 [50%])
		.respawn_rate = 12;		// Every x hours (max: 12)
	
	end;
	
}


/*-----------------------------------------------------
Duplicates
-----------------------------------------------------*/
pay_dun00,54,147,0	duplicate(mining)	Variant Mineral#1	S_EMPEL_1
pay_dun00,55,147,0	duplicate(mining)	Variant Mineral#2	S_EMPEL_1
pay_dun00,53,146,0	duplicate(mining)	Variant Mineral#3	S_EMPEL_2
pay_dun00,53,145,0	duplicate(mining)	Variant Mineral#4	S_EMPEL_1

pay_dun00,69,148,0	duplicate(mining)	Variant Mineral#5	S_EMPEL_2
pay_dun00,70,147,0	duplicate(mining)	Variant Mineral#6	S_EMPEL_1
pay_dun00,70,146,0	duplicate(mining)	Variant Mineral#7	S_EMPEL_1

pay_dun00,84,140,0	duplicate(mining)	Variant Mineral#8	S_EMPEL_1
pay_dun00,85,140,0	duplicate(mining)	Variant Mineral#9	S_EMPEL_2
pay_dun00,86,139,0	duplicate(mining)	Variant Mineral#10	S_EMPEL_1

pay_dun00,83,126,0	duplicate(mining)	Variant Mineral#11	S_EMPEL_1

pay_dun00,60,125,0	duplicate(mining)	Variant Mineral#12	S_EMPEL_2

pay_dun00,46,124,0	duplicate(mining)	Variant Mineral#13	S_EMPEL_1
pay_dun00,45,123,0	duplicate(mining)	Variant Mineral#14	S_EMPEL_2

pay_dun00,53,117,0	duplicate(mining)	Variant Mineral#15	S_EMPEL_1
pay_dun00,53,118,0	duplicate(mining)	Variant Mineral#16	S_EMPEL_2

pay_dun00,44,109,0	duplicate(mining)	Variant Mineral#17	S_EMPEL_2

pay_dun00,50,69,0	duplicate(mining)	Variant Mineral#18	S_EMPEL_1
pay_dun00,51,70,0	duplicate(mining)	Variant Mineral#19	S_EMPEL_2

pay_dun00,35,54,0	duplicate(mining)	Variant Mineral#20	S_EMPEL_1

pay_dun00,75,84,0	duplicate(mining)	Variant Mineral#21	S_EMPEL_1
pay_dun00,75,85,0	duplicate(mining)	Variant Mineral#22	S_EMPEL_1
pay_dun00,76,86,0	duplicate(mining)	Variant Mineral#23	S_EMPEL_2

pay_dun00,123,100,0	duplicate(mining)	Variant Mineral#24	S_EMPEL_2
pay_dun00,123,101,0	duplicate(mining)	Variant Mineral#25	S_EMPEL_2
pay_dun00,124,102,0	duplicate(mining)	Variant Mineral#26	S_EMPEL_2

pay_dun00,131,157,0	duplicate(mining)	Variant Mineral#27	S_EMPEL_1
pay_dun00,132,158,0	duplicate(mining)	Variant Mineral#28	S_EMPEL_2

pay_dun00,139,164,0	duplicate(mining)	Variant Mineral#29	S_EMPEL_2
pay_dun00,139,165,0	duplicate(mining)	Variant Mineral#30	S_EMPEL_1
pay_dun00,140,166,0	duplicate(mining)	Variant Mineral#31	S_EMPEL_1
pay_dun00,141,166,0	duplicate(mining)	Variant Mineral#32	S_EMPEL_2

pay_dun00,147,171,0	duplicate(mining)	Variant Mineral#33	S_EMPEL_1
pay_dun00,147,172,0	duplicate(mining)	Variant Mineral#34	S_EMPEL_2
pay_dun00,147,173,0	duplicate(mining)	Variant Mineral#35	S_EMPEL_1
pay_dun00,148,174,0	duplicate(mining)	Variant Mineral#36	S_EMPEL_2
pay_dun00,149,174,0	duplicate(mining)	Variant Mineral#37	S_EMPEL_1

pay_dun00,154,174,0	duplicate(mining)	Variant Mineral#38	S_EMPEL_1
pay_dun00,155,174,0	duplicate(mining)	Variant Mineral#39	S_EMPEL_2
pay_dun00,156,173,0	duplicate(mining)	Variant Mineral#40	S_EMPEL_1
pay_dun00,156,172,0	duplicate(mining)	Variant Mineral#41	S_EMPEL_2
pay_dun00,156,171,0	duplicate(mining)	Variant Mineral#42	S_EMPEL_2
pay_dun00,156,170,0	duplicate(mining)	Variant Mineral#43	S_EMPEL_1
pay_dun00,156,169,0	duplicate(mining)	Variant Mineral#44	S_EMPEL_2
