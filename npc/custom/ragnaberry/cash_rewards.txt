// Winterfox - Cash Rewards V 1.0
-	script	cash_rewards	FAKE_NPC,{
	OnInit:
		// To add manual mobs besides MVPs.
		.@mobIdListStr$ = "1059, 1647, 1511, 1651, 1650, 1785, 1039, 1399, 1874, 1086, 1871, 2068, 1502, 1251, 1719, 1046, 1389, 1112, 1115, 1658, 1157, 1150, 1159, 1312, 1885, 1252, 1832, 1734, 1779, 1980, 1630, 1688, 1991, 1646, 1990, 1147, 1708, 1648, 1917, 1087, 1038, 1768, 1623, 1492, 1272, 1373, 1190, 1418, 2022, 1649, 1802, 1583, 1751, 1685";
 
		// To filter certain mobs.
		.@mobFilterListStr$ = "";
 
		// Set the points you get for slaying an mob defined in the rewardMobsIds array.
		.mobRewardPoints = 10;
 
		// Set the points you get for slaying an opponent in pvp
		//.pvpRewardPoints = 0;
 
		// Explode the mob id list.
		explode(.@mobIdList$, .@mobIdListStr$, ", ");
 
		// Loop trough the id list.
		for(.@i = 0; .@i < getarraysize(.@mobIdList$); .@i++) {
			// Rebuild add the id to the mob ids.
			.@mobIds[.@i] = atoi(.@mobIdList$[.@i]);
		}
 
		// Explode the mob filter list
		explode(.@mobFilterList$,.@mobFilterListStr$,", ");
 
		// Loop trough the filter list.
		for(.@i = 0; .@i < getarraysize(.@mobFilterList$); .@i++) {
			// Rebuild it to another array with id as key.
			.@mobFilter[atoi(.@mobFilterList$[.@i])] = 1;
		}
 
		// Loop trough the mob ids.
		for(.@i = 0; .@i < getarraysize(.@mobIds); .@i++) {
			// If mob id is not in the filter.
			if(!.@mobFilter[.@mobIds[.@i]]) {
				// Add it to the rewards.
				.rewardMobIds[.@mobIds[.@i]] = 1;
			}
		}
 
		// Search all MVPs based on if a mob gives MVP Exp.
		.@db = query_sql("SELECT ID  FROM `mob_db` WHERE `MEXP` > 0", .@mvpIds);
		// Loop trough all MVPs
		for(.@i = 0; .@i < getarraysize(.@mvpIds); .@i++) {
			// If the MVP is not in the filter.
			if(!.@mobFilter[.@mvpIds[.@i]]) {
				// Add it to the rewards.
				.rewardMobIds[.@mvpIds[.@i]] = 1;
			}
		}
	end;
	OnNPCKillEvent:
		// If killed mob is a monster that gives a reward.
		if(.rewardMobIds[killedrid]) {
			// Add Cash Point reward.
			#CASHPOINTS += .mobRewardPoints;
 
			// Globally announce the kill.
			announce strcharinfo(PC_NAME) + " derrotou " + strmobinfo(1, killedrid) + " e ganhou "+ .mobRewardPoints +" rBerry Point" + ((.mobRewardPoints > 1) ? "s" : "") + ".", bc_all;
			
			// Show Cash Point gain to the player.
			dispbottom "Agora você tem " + #CASHPOINTS + " rBerry Point" + ((#CASHPOINTS > 1) ? "s" : "") + ".";
		
		}
	end;
	//OnPCKillEvent:
			// Add Cash Point reward.
			//#CASHPOINTS += .pvpRewardPoints;
 
			// Show Cash Point gain to the player.
			//dispbottom "You now have " + #CASHPOINTS + " Cash Point" + ((#CASHPOINTS > 1)? "s":"") + ".";
 
			// Announce the kill on the map.
			//announce strcharinfo(PC_NAME) + " killed " + rid2name(killedrid) + " and earned " + .pvpRewardPoints +" Cash Point" + ((.pvpRewardPoints > 1)? "s" : "") + ".", bc_map;
	//end;
}